options nomprint;

libname	l1		"/share/sfrisk/legal/1_eligible";
libname	l2		"/share/sfrisk/legal/2_li" ;
libname	l4 		"/share/sfrisk/legal/4_inventory";
libname	l5		"/share/sfrisk/legal/5_kabarai" ;
libname 	home	"~/";
libname ddb '~/dairi_DB';

%include "/odd/lib/sf-dwh.key";


/*%%%%%%%%%% CP Manual Update %%%%%%%%%%*/

/* yymm155=1211
   yymm182=1502*/

%let s_cut = 155 ;  	/*CHANGE*/
%let e_cut = 182 ;		/*CHANGE*/


/*** Initial Date ***/
%let ST='01jan2000'd; 	/*Note: '01jan2000'd --> s_cut=1*/
       
data _null_;		/*n= months elapsed from s_cut=1 to today() nはs_cut=1～本日の間の経過月数*/
        call symput ("n",intck("month",&ST-1,intnx('day',intnx('month',today(),0),-1))) ;
run;

/***** Check *****/
%put %nrstr(&n =) &n;
data _null_;	/*creates monthly macvar series from '01jan2000'd to today()*/
	%macro YM1;
		%do i= 0 %to &n ;
			call symput("yymm&i" ,put(intnx("month",&ST-1,%eval(&i),"end"),yymmdd4.));
		%end;
	%mend YM1;
	%YM1 ;
run;

%macro YM2 ; /*output to log all yymm&i macvar*/
	%do i= 0 %to &n ;
		%put yymm&i = &&yymm&i;
	%end ;
%mend YM2;
%YM2 ;


/*-----------------START MONTHLY INVENTORY REPORT---------------------------*/
%macro inv1 ;

%do i = &s_cut %to &e_cut;

proc sort data=l5.disclosure&&yymm&i(keep=serialno kabarai_claim_amount) /*過払い請求金額*/
	out=disclosure&&yymm&i; by serialno; run;

data	inventory&&yymm&i ; /*14年04月以降、抽出方法が変更になった為、/share/sfrisk/legal/4_inventoryのin_process_YYMM.sas7bdat.gzからInv用ファイルを再現*/
	set in_process_&&yymm&i /*CHANGE home.*/
		(where=(gz_seg not in("NA","New_W","W")	and /*Incl. A1R,A2,B,C*/
		kabarai_inventory=1	and
		book in("OB","WO","PO","PO5Y","PO10Y") and
		portfolio in("PL","WC","XS","AF") and /*Excl. BK,MG,SN*/
		(c027 in (19,20,55,70,76,80) or  /*19=弁護士交渉中 20=弁護士受訴 55=司法書士介入 70=代理権付司法書士 76=訴訟（除く当社申立 80=本人及び公的機関の過払主張*/
		 c028 in (19,20,55,70,76,80) or
		 c029 in (19,20,55,70,76,80) or
		 c030 in (19,20,55,70,76,80) or
		 c031 in (19,20,55,70,76,80))));
		 
length gz_seg1 $3;
if substr(gz_seg,1,1)='C' then gz_seg1='C';
else if substr(gz_seg,1,1)='B' then gz_seg1='B';
else if substr(gz_seg,1,3)='A-1' or substr(gz_seg,1,1)='R' then gz_seg1='A1R';
else if substr(gz_seg,1,3)='A-2' then gz_seg1='A2';
else if substr(gz_seg,1,3)='New' or substr(gz_seg,1,1)='W' then gz_seg1='W';
else gz_seg1=gz_seg;

	/***** Pre & Post Flag: Overwrite preexisting trans_flag ---- 既存trans_flagを上書き *****/
	*length		trans_flag	$6. ; 
	if	c042 = '31Dec9999'd or c042=.	then trans_flag = 'NA' ; /*C042=当初貸付年月日*/
	else if	c042<='30Sep1993'd 			then trans_flag = 'pre93';
	else trans_flag = 'post93';
run ;

/*Pool限定 5,317,830件*/
proc sort data=inventory&&yymm&i presorted;by serialno;run ;

data	inventory&&yymm&i ;
   merge inventory&&yymm&i(in=t1)
		 home.poollist(in=t2);
by serialno;
if t1 and t2;
run;


data	frie ; /*Create dataset where negotation is ongoing */
set	risklib.frie&&yymm&i
	(keep= 	SERIALNO
		F&&yymm&i..003
		F&&yymm&i..004
		F&&yymm&i..009
		F&&yymm&i..010
		F&&yymm&i..011
		F&&yymm&i..012
	rename=(F&&yymm&i..003=	F003
		F&&yymm&i..004=	F004
		F&&yymm&i..009=	F009
		F&&yymm&i..010=	F010
		F&&yymm&i..011=	F011
		F&&yymm&i..012=	F012)
		
where=(hour(F004)>=8 and hour(F004)<=21 and
	F009 in ("0201","0202") and /*当方より電話,先方より電話*/
	F011 in ("0310","0323","0329","0331") and /*弁護士, 司法書士, 裁判所＆弁護士, 裁判所＆司法書士*/
	F010 in ("0403","0404","0408","0417","0418") and /*弁護士事務所, 裁判所,司 法書士事務所, 裁判所＆弁護士, 裁判所＆司法書士*/
	F012 not in("0501","0502","0504","0505","0506","0509","0510","0511","0514","0515","0516","0517","0518","0519")));
run ;

proc sort data=frie nodupkey out = frie_nego&&yymm&i;
by serialno ;
run ;

/*-----*/
data	inventory&&yymm&i ;
merge	inventory&&yymm&i	(in=a)
	frie_nego&&yymm&i	(in=b	keep=serialno) 
	disclosure&&yymm&i 		(in=c keep=serialno kabarai_claim_amount);

by	serialno ;
if	a ;
if	b	then	in_nego=1;/*Determine a/c where negotation is ongoing and not already in Inventory dataset*/
else			in_nego=0;

if	kabarai_claim_amount=. or kabarai_claim_amount=0 then claim_amt_flag=0;
else claim_amt_flag=1 ;

if maxirrl_balance=. then kaiji = -1;
else if maxirrl_balance>= 0 then kaiji = 1;
else kaiji = 0;

if MaxIRRL_Balance=-1 then MaxIRRL='-1';
else if MaxIRRL_Balance<0 then MaxIRRL='-';
else if MaxIRRL_Balance=0 then MaxIRRL='0';
else if MaxIRRL_Balance>0 then MaxIRRL='+';

/*****社内メッセージ２ M2 *****/
array SM2{*} M2_1 - M2_5;
do i=1 to dim(SM2);
	if SM2(i) in(345,809,324,803) then delete; /*	345=ＩＲＲＬマイナス／プラス和解済, 
													809=過払返還請求振込済, 
													324=過払返還請求０和解済, 
													803=過払返還請求解決*/
	else if SM2(i)=806 and M2^=806 then M2=806;			/*806=過払返還請求交渉中*/
	else if M2^=806 then M2=0;							/*must incl. M2^=806 otherwise value is overwritten in subsequent iterations*/
end;
	
/***** M2_Date *****/
if M2=806 then do;
	array M2array{*} M2_1 - M2_5;
	array M2D{*} M2_1_DT -- M2_5_DT;
	do i=1 to dim(M2array);
		if M2array(i)=806 then M2_Date=M2D(i);
	end;
end;
else M2_Date=.;
run;

proc sql;
drop table disclosure&&yymm&i,frie,frie_nego&&yymm&i;quit;
/*-----*/

/*-----------------External lawyer check-----------------*/
data _null_;
a = mdy(%substr(&&yymm&i,3,2), 1, %substr(&&yymm&i,1,2));
call symput("EOM",put(intnx("month", a, 0, "end"), YYMMDDn8.));
run;
	
%put YYMM = &&yymm&i EOM = &EOM;


%if &i=&s_cut %then %do;
proc sql;
	create table LitiDB as
	select 
		_col0 as serialno,
		_col65 as lawyer_type,	/*委任状況区分*/
		_col62 as rec_date	/*委任状況入力日*/
	from oralib.VEW_KSSJHREC_N001; /*訴訟DB*/
quit;

proc sort data=LitiDB(where=(rec_date<=&EOM and lawyer_type = 4)) out=liti&&yymm&i /*nodupkey*/; /*pick record with most recent LI date*/
by serialno descending rec_date;run;

data liti&&yymm&i;
	set liti&&yymm&i;
by serialno descending rec_date;
if first.serialno=1 then output;
run;
%end;

%else %if &i>&s_cut %then %do;
proc sort data=LitiDB(where=(rec_date<=&EOM and lawyer_type = 4)) out=liti&&yymm&i /*nodupkey*/;/*pick record with most recent LI date*/
by serialno descending rec_date;run;

data liti&&yymm&i;
	set liti&&yymm&i;
by serialno descending rec_date;
if first.serialno=1 then output;
run;
%end;

data inventory&&yymm&i;
	merge inventory&&yymm&i.(in=t1) liti&&yymm&i;
by serialno;
if t1;
run;

proc sql;
drop table liti&&yymm&i;quit;

%if &i=&s_cut %then %do;
 data work.kaijiDB(rename=(_COL0=serialno /*会員一連番号*/
							_COL19=wakaibi)); /*相手方当初主張金額 >>>var error according to Patrick, date not amt*/
 set oralib.VEW_JKJSJREC_N001(keep=_COL0 _COL19);/*開示進捗DB*/
 run;

 proc sql;
  create table inventory&&yymm&i as
 select
	t1.*,t2.wakaibi
 from inventory&&yymm&i as t1 left join kaijiDB as t2 on t1.serialno=t2.serialno;
 quit;

%end;

%else %if &i>&s_cut %then %do;
 proc sql;
  create table inventory&&yymm&i as
 select
	t1.*,t2.wakaibi
 from inventory&&yymm&i as t1 left join kaijiDB as t2 on t1.serialno=t2.serialno;
 quit;
%end;



/*-------Flag 9 firm external lawyer litigations-----------------------*/
data inventory&&yymm&i;
	set inventory&&yymm&i;

label wakaibi = "和解日";
length wakaitsuki 6.;
wakaitsuki = substr(put(wakaibi,z8.),3,4);
if wakaitsuki < &&yymm&i then do;
wakaitsuki = 0;end; 

length category $15.;
if (M2=806 and Claim_Type = 'Liti') or    /*806=過払返還請求交渉中 bl(1) ブル要因区分 in(20,76) 20=弁護士受訴 76=訴訟（除く当社申立）*/
   (M2=0 and Claim_Type ='Liti' and kaiji^=1) then category='Litigation';	/*任意*/
else if M2=806 and Claim_Type ='Non_Liti' then category='Non Litigation';	 /*訴外*/
else if M2=0 and kaiji^=1 then category='Without Claim'; /*開示請求なし*/
else category='NA';
run;

proc sort data=inventory&&yymm&i noduprec;
by serialno;
run;


/*-------Add Law Office Names------------------*/
 
/* ----------Agent_noで代理人DBと結合する為、介入DBを参照し、InventoryファイルにAgent_Noを作成----------------	*/
	data _null_;
		a = mdy(%substr(&&yymm&i,3,2), 1, %substr(&&yymm&i,1,2));
		/*b = put(a, date9.); delete*/
		b = put(intnx("month", a, 0, "end"), date9.);
		call symput("eom", b);
	run;

	%put EOM = &eom;

%if &i=&s_cut %then %do;
data work.li(rename=(
			_COL0 = no
			_COL1 = serialno
			_COL3 = l_no	/*弁護士番号*/
			_COL4 = l_in_date	/*弁護士介入年月日*/
			_COL5 = l_out_date	/*弁護士辞任年月日*/
			_COL6 = s_no	/*司法書士番号*/
			_COL7 = s_in_date	/*司法書士介入年月日*/
			_COL8 = s_out_date));	/*司法書士辞任年月日*/
set oralib.TBN_SBSHKREC(drop=_COL2) /*店番*/;	/* 介入DB */
run;

data work.li;
	set work.li;
if l_no ne 0 then do;
	if datepart(l_in_date) <= "&eom"d <= datepart(l_out_date) then
		agent_no = compress("L" || l_no); end;

if agent_no = '' and s_no ne 0 then do;
	if datepart(s_in_date) <= "&eom"d <= datepart(s_out_date) then
		agent_no = compress("S" || s_no);end;
		
RP= (agent_no = '');		/* Right Party フラグ */
run;

%end;

%else %if &i>&s_cut %then %do;
data work.li;
	set work.li;

if l_no ne 0 then do;
	if datepart(l_in_date) <= "&eom"d <= datepart(l_out_date) then
		agent_no = compress("L" || l_no); end;

if agent_no = '' and s_no ne 0 then do;
	if datepart(s_in_date) <= "&eom"d <= datepart(s_out_date) then
		agent_no = compress("S" || s_no); end;
		
RP= (agent_no = '');		/* Right Party フラグ */
run;

%end;

proc sort data=work.li; by serialno;run;
data inventory&&yymm&i;
merge  inventory&&yymm&i(in=t1) 
		work.li (in=t2 keep=serialno agent_no RP);
by serialno;
if t1;

if missing(RP) and Claim_Person='RP' then RP=1;
run;


/* ------------------------最新代理人DBと結合------------------------*/
proc sort data=inventory&&yymm&i; by agent_no;run;	

data inventory&&yymm&i;
		merge	inventory&&yymm&i(in=t1)
			ddb.ddb1502(keep=Agent_No GC GN);			/*CHANGE*/  
		by	agent_no;
if t1; 				/*新宿グループ*/
if  GC='G10' or 	/*G10	G_新宿事務所*/
	GC='G5455' or	/*G5455	G_東京新宿法律事務所*/
	GC='G6255' or	/*G6255	G_有岡法律事務所*/
	GC='G1366' or	/*G1366	G_ＭａｒｔｉａｌＡｒｔｓ*/
	GC='G24776' or	/*G24776	G_法律事務所ナビアス*/
	GC='G5071'		/*G5071	G_大江戸下町法律事務所*/
	then Jimusho='Shinjuku';
else Jimusho='Other';
	run;
/* ---------*/


/*Summarize Important inventory variables*/
/*必要な変数を集計*/

proc sql ;
create table sum_inventory&&yymm&i as 
select
	c003 as YM format=YYMMDD10.,
	/*def,
	SOL,*/
	Portfolio,
	gz_seg,
	gz_seg1,
	Book,
	M2,
	RP,
	irrl_flag,
	MaxIRRL,
	Kaiji,
	claim_Type,
	lawyer_type,
	Jimusho,
	kabarai_inventory,
	ac15 as wakai_flag,
	se004,
	trans_flag,
	in_nego,
	claim_amt_flag,
	Passed_Bucket,
	count(serialno) as count,
	sum(irrl_balance/1000000) as Irrl_mm,
	sum(ac28/1000000) as Bal_mm
from		
	inventory&&yymm&i(where=(SOL='NA')) /*CHANGE*/
group by
	YM,
	/*def,
	SOL,*/
	Portfolio,
	gz_seg,
	gz_seg1,
	Book,
	M2,
	RP,
	irrl_flag,
	MaxIRRL,
	Kaiji,
	claim_Type,
	lawyer_type,
	Jimusho,
	kabarai_inventory,
	wakai_flag,
	se004,
	trans_flag,
	in_nego,
	claim_amt_flag,
	Passed_Bucket;
quit;
run;
 
%end;		/* End of i Loop */


/* Create summary file and recreate Category variable in summary file */
/* サマリーファイルを作成,サマリーファイルにCategory変数を再現*/
data	sum_inventory ;
set
	%do i = &s_cut %to &e_cut ;
	sum_inventory&&yymm&i
	%end ;
	;
run ;

data sum_inventory;
	set sum_inventory;

length category $15.;
if (M2=806 and Claim_Type = 'Liti') or  /*806=過払返還請求交渉中 bl(1) ブル要因区分 in(20,76) 20=弁護士受訴 76=訴訟（除く当社申立）*/
   (M2=0 and Claim_Type ='Liti' and kaiji^=1) then category='Litigation';	/*任意*/
else if M2=806 and Claim_Type ='Non_Liti' then category='Non Litigation';	 /*訴外*/
else if M2=0 and kaiji^=1 then category='Without Claim'; /*開示請求なし*/
else category='NA';


proc export data = sum_inventory
OUTFILE= "~/sum_inventory201502.csv"  /*CHANGE*/
dbms = csv replace;
run;

%mend inv1;
%inv1


/*-----------------END MONTHLY INVENTORY REPORT---------------------------*/



/*-----------------START INVENTORY WALK REPORT-----------------------------*/
/*%let s_cut=161;  testing purpose*/


%macro inv2;

%do i = &s_cut %to &e_cut;

data inventory_w&&yymm&i;
	set inventory&&yymm&i(where=(SOL='NA')); /*CHANGE*/
if category ='NA' then delete; /* Logic ?*/
run;

proc sort data=inventory_w&&yymm&i;by serialno;run; 
%end ;		/* End of i Loop */

%mend inv2;
%inv2


/*------------------------------*/

%let s_cut =%eval(&s_cut+1) ; /*+1 month from s_cut macvar defined above in Monthly Inventory*/


%macro inv3;

%do i = &s_cut %to &e_cut;
 %let j = %eval(&i-1);

data inventory_w&&yymm&i drop&&yymm&i;
	merge inventory_w&&yymm&i(in=t1 keep=serialno category wakaitsuki mos_li trans_flag Jimusho /*new*/ RP portfolio gz_seg1 book) 
		  inventory_w&&yymm&j(in=t2 rename=(category=category&&yymm&j wakaitsuki=wakaitsuki&&yymm&j mos_li=mos_li&&yymm&j Jimusho=Jimu&&yymm&j
											/*new*/ RP=RP&&yymm&j portfolio=portfolio&&yymm&j gz_seg1=gz_seg1&&yymm&j book=book&&yymm&j));
by serialno;
if t2 and not t1 then output drop&&yymm&i;/*all previous month INV NOT in current month INV*/
else output inventory_w&&yymm&i;/*all existing inventory from previous month + all new inventory from current month*/
run;

data drop&&yymm&i;
	merge drop&&yymm&i(in=t1) 
		  l1.cust&&yymm&i(keep=serialno c027 c028 c029 c030 c031 
						  rename=(c027=c027_1 c028=c028_1 c029=c029_1 c030=c030_1 c031=c031_1));
by serialno;
if t1;
run;

data drop&&yymm&i;
	set drop&&yymm&i;
	
length out_category $10.;

if wakaitsuki&&yymm&j = &&yymm&i or wakaitsuki&&yymm&j = &&yymm&j then out_category ='Wakai';
else if c027_1 not in (19,55,70) and 
		c028_1 not in (19,55,70) and
		c029_1 not in (19,55,70) and
		c030_1 not in (19,55,70) and
		c031_1 not in (19,55,70) then out_category = 'Jinin';
else if mos_li&&yymm&j eq 23 then out_category = "LI 2yr+";
else out_category ='Other';
run;

/* OUTPUT CHECK
proc freq data=drop&&yymm&i;categirt
tables out_category / nocum;
title "&&yymm&i";run;
*/

data all&&yymm&i;
	set drop&&yymm&i(keep=serialno trans_flag out_category category&&yymm&j Jimu&&yymm&j /*new*/ RP&&yymm&j portfolio&&yymm&j gz_seg1&&yymm&j book&&yymm&j
					 rename=(category&&yymm&j = prev_category Jimu&&yymm&j=Jimusho /*new*/ RP&&yymm&j=RP portfolio&&yymm&j=portfolio gz_seg1&&yymm&j=gz_seg1 book&&yymm&j=book)) 
	    inventory_w&&yymm&i(keep=serialno trans_flag category mos_li category&&yymm&j Jimusho Jimu&&yymm&j /*new*/ RP portfolio gz_seg1 book 
							rename=(category&&yymm&j = prev_category));
							            /*current period INV vars*/ /*prev period existing INV vars*******/
length inv_status $10. yymm count 4.;

if mos_li = 0 then inv_status ='New LI';
else if prev_category ne category then inv_status ='Shift';
else if prev_category eq category then inv_status ='No Change';
else inv_status ='Check';

if inv_status^='New LI' then do;  /*従来より間単にピボット化するため、下記の追加変数を作成*/
	if prev_category='Without Claim' and category='Without Claim' then inv_Detail="w/oC > w/oC";
		else if prev_category='Without Claim' and category='Non Litigation' then inv_Detail="w/oC > NL";
		else if prev_category='Without Claim' and category='Litigation' then inv_Detail="w/oC > L";
		else if prev_category='Without Claim' and category='' then inv_Detail="w/oC > null";
		
	else if prev_category='Non Litigation' and category='Non Litigation' then inv_Detail='NL > NL';
		else if prev_category='Non Litigation' and category='Without Claim' then inv_Detail='NL > w/oC';
		else if prev_category='Non Litigation' and category='Litigation' then inv_Detail='NL > L';
		else if prev_category='Non Litigation' and category='' then inv_Detail='NL > null';
	
	else if prev_category='Litigation' and category='Litigation' then inv_Detail='L > L';
		else if prev_category='Litigation' and category='Without Claim' then inv_Detail='L > w/oC';
		else if prev_category='Litigation' and category='Non Litigation' then inv_Detail='L > NL';
		else if prev_category='Litigation' and category='' then inv_Detail='L > null';
	
	else if prev_category='' and category='' then inv_Detail='null > null';
		else if prev_category='' and category='Without Claim' then inv_Detail='null > w/oC';
		else if prev_category='' and category='Non Litigation' then inv_Detail='null > NL';
		else if prev_category='' and category='Litigation' then inv_Detail='null > L';
	end;

if Inv_Status in ('No Change','Check') and Jimu&&yymm&j^='' then Jimusho=Jimu&&yymm&j;/*align Jimusho variables*/
drop Jimu&&yymm&j;

yymm = &&yymm&i;
run;
/* OUTPUT CHECK
proc freq data=all&&yymm&i;
tables inv_status /nocum;
title "&&yymm&i";run;
*/

data all&&yymm&i;
set all&&yymm&i;
length Category1 $13;
if category in ('Litigation','Non Litigation') then Category1='With Claim';
else if category='Without Claim' then Category1='Without Claim';
else if missing(category) then Category1='Shift';
run;

proc sql;
create table walk_inventory&&yymm&i as
select yymm, 
/*new*/ RP,portfolio, gz_seg1, book, 
out_category, category1,category, prev_category, inv_status, inv_detail, trans_flag, Jimusho, count(serialno) as Count
from all&&yymm&i
group by yymm, /*new*/ RP, portfolio, gz_seg1, book, out_category, category1,category, prev_category, inv_status,  inv_detail, trans_flag, Jimusho, Count
order by yymm, /*new*/ RP,portfolio, gz_seg1, book, out_category, category1,category, prev_category, inv_status,  inv_detail, trans_flag, Jimusho, Count;
quit;

%end ;		/* End of i Loop */

data	sum_walk ;
set
	%do i = &s_cut %to &e_cut ;
	walk_inventory&&yymm&i
	%end;
	;
run;

proc export data = sum_walk
OUTFILE= "~/sum_walk201502.csv" /*CHANGE*/
dbms = csv replace ;
run ;


%mend inv3;
%inv3


/*-----------------END INVENTORY WALK REPORT-----------------------------*/


/*BOP 2011/12*/
proc freq data=inventory_w1112;
tables category;
title "BOP";
run;

/*---------------------------------------------------------------------------*/

/*-----------------------------Data check------------------------------------*/

proc export data = all1306
OUTFILE= "~/walk1306.csv"
dbms = csv replace ;
run ;




/*Exploratory*/

proc freq data=inventory_w1410;
tables gn*jimusho / nocol nopercent norow nocum;
where Category="Non Litigation";
run;

proc sql;
create table aa_inv_w1410 as
select GN,count(GN) as Count
from inventory_w1410 
where Category="Non Litigation"
group by GN
order by Count;
quit;
